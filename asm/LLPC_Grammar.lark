start: namespace | ex_decl+

namespace: NAMESPACE ident LCURLY ex_decl+ RCURLY

ex_decl: struct_decl 
       | fn_decl

//include_dir: INCLUDE ident

struct_decl: STRUCT ident LCURLY struct_expr RCURLY
struct_expr: args fn_decl* [struct_expr]

fn_decl: FN ident LPAREN [args] RPAREN type stmt

stmt: blockstmt 
    | declexpr
    | declstmt
    | exprstmt
    | jumpstmt
    | iterstmt
    | selstmt
    | assumestmt

assumestmt: ASSUME assumptionbody SEMICOLON
assumptionbody: "unreachable" -> assumeunreachable

blockstmt: LCURLY stmt* RCURLY
encexprsmt: [expr] [SEMICOLON]
exprstmt: [expr] SEMICOLON

declstmt: VAR ident COLON type SEMICOLON
declexpr: VAR ident [COLON type] EQUAL expr SEMICOLON
expr: assgexpr
?assgexpr: lorexpr
        | postexpr assgop [BTICK] assgexpr
?lorexpr: landexpr
        | lorexpr OR landexpr
?landexpr: lunaryexpr
        | landexpr AND lunaryexpr
?lunaryexpr: relexpr
	| EMARK lunaryexpr
?relexpr: borexpr 
	| borexpr relop borexpr
?borexpr: bxorexpr
	| TRUE -> true
	| FALSE -> false
	| "overflow" -> overflow
        | borexpr PIPE [BTICK] bxorexpr
?bxorexpr: bandexpr
        | bxorexpr CARROT [BTICK] bandexpr
?bandexpr: addexpr
        | bandexpr AMPERSAND [BTICK] addexpr
?addexpr: multexpr
        | addexpr addop [BTICK] multexpr
?multexpr: castexpr
        | multexpr multop [BTICK] castexpr
?castexpr: unaryexpr
        | "<" type ">" castexpr
?unaryexpr: postexpr
        | unaryop castexpr
?postexpr: primexpr
	| AT ident LPAREN [argexprs] RPAREN -> intrinsic
        | ident LCURLY [structassgs] RCURLY -> structinit
        | postexpr LBRACK expr PLUSCOLON expr RBRACK -> sliceexpr
        | postexpr LBRACK expr RBRACK -> indexpr
        | postexpr LPAREN [argexprs] RPAREN -> callexpr
        | postexpr DOT ASTERISK -> derefexpr
        | postexpr DOT AMPERSAND -> addrexpr
        | postexpr DOT QMARK -> denullexpr
        | postexpr DOT ident -> fieldexpr
        | postexpr COLON ident -> colonexpr
        | BITSIZEOF LPAREN type RPAREN -> bitsizeof
        | ELEMSIZEOF LPAREN type RPAREN -> elemsizeof
	| BREAKPOINT -> breakpoint
?primexpr: constant
        | ident
        | string
        | LPAREN expr RPAREN
?jumpstmt: CONTINUE SEMICOLON -> continue
	| BREAK SEMICOLON -> break
	| RETURN SEMICOLON -> return
	| RETURN expr SEMICOLON -> returnexpr
?iterstmt: whileelsestmt
	| whilestmt
	| forelsestmt
	| forstmt
	| doelsestmt
	| dostmt
?selstmt: ifelsestmt
	| ifstmt

structassgs: structassg (COMMA structassg)* COMMA?
structassg: ident COLONEQUALS assgexpr

whileelsestmt: WHILE LPAREN expr RPAREN stmt ELSE stmt
whilestmt: WHILE LPAREN expr RPAREN stmt
forelsestmt: FOR LPAREN stmt exprstmt encexprsmt RPAREN stmt ELSE stmt
forstmt: FOR LPAREN stmt exprstmt encexprsmt RPAREN stmt
doelsestmt: DO LPAREN expr RPAREN stmt ELSE stmt
dostmt: DO LPAREN expr RPAREN stmt

ifelsestmt: IF LPAREN expr RPAREN stmt ELSE stmt
ifstmt: IF LPAREN expr RPAREN stmt

argexprs: assgexpr (COMMA assgexpr)* COMMA?
args: arg (COMMA arg)* COMMA?
arg: ident COLON type

ident: /[_a-zA-Z][_a-zA-Z0-9]*/
identtype: ident ASTERISK* [LBRACK constant RBRACK]
type: INT | VOID | identtype
string: /"([^\x00"]*\x01)*[^\x01"]*"/
//string: /"[^"]*"/
constant: CHAR | hexint | decint

decint: "0d" UD* | D UD*
hexint: "0x" UH*
CHAR: /\'(?:.|(?:\\.)|(?:\\x..))\'/

INT: /[ui][0-9]+(?: *)\**(\[[0-9]+\])?/

assgop: EQUAL | PLUSWRAPEQUAL | MINUSWRAPEQUAL | PLUSEQUAL | MINUSEQUAL | LANGLEEEEQUAL | RANGLEEEEQUAL | LANGLEEWRAPEQUAL | LANGLEEEQUAL | RANGLEEEQUAL | PERCENTEQUAL | SLASHEQUAL | ASTERISKEQUAL | AMPERSANDEQUAL | PIPEEQUAL | CARROTEQUAL
relop: LANGLEQ | RANGLEQ | EQUALL | NEQUAL | LANGLE | RANGLE
addop: PLUSWRAP | MINUSWRAP | PLUS | MINUS
multop: LANGLEEE | RANGLEEE | LANGLEEWRAP |LANGLEE | RANGLEE | PERCENT | SLASH | ASTERISKWRAP | ASTERISK
unaryop: PLUS | MINUS | TILDE

NAMESPACE: "namespace"
BTICK: "`"
BREAKPOINT: "BREAKPOINT"
VOID: "void"
EQUAL: "="
LEFTARROW: "CAST"
FN:  "fn"
LPAREN: "("
RPAREN: ")"
LCURLY: "{"
RCURLY: "}"
COMMA: ","
COLON: ":"
SEMICOLON: ";"
DOT: "."
LBRACK: "["
RBRACK: "]"
ASTERISK: "*"
ASTERISKWRAP: "*%"
AMPERSAND: "&"
QMARK: "?"
OR: "or"
AND: "and"
PIPE: "|"
CARROT: "^"
PLUS: /\+(?!:)/
PLUSWRAP: "+%"
PLUSCOLON: "+:"
PLUSEQUAL: "+="
PLUSWRAPEQUAL: "+%="
MINUS: "-"
MINUSWRAP: "-%"
MINUSEQUAL: "-="
MINUSWRAPEQUAL: "-%="
LANGLEEEEQUAL: "<<<=" 
RANGLEEEEQUAL: ">>>=" 
LANGLEEEQUAL: "<<=" 
LANGLEEWRAPEQUAL: "<<%=" 
RANGLEEEQUAL: ">>=" 
PERCENTEQUAL: "%=" 
SLASHEQUAL: "/=" 
ASTERISKEQUAL: "*=" 
AMPERSANDEQUAL: "&=" 
PIPEEQUAL: "|=" 
CARROTEQUAL: "^=" 
LANGLEEE: "<<<"
LANGLEE: "<<"
LANGLEEWRAP: "<<%"
LANGLE.10: /<(?![:=<])/
RANGLEEE: ">>"
RANGLEE: ">>"
RANGLE: ">"
PERCENT: "%"
SLASH: "/"
TILDE: "~"
EMARK: "!"
LANGLEQ: "<="
RANGLEQ: ">="
NEQUAL: "!="
EQUALL: "=="
VAR: "var"
CONTINUE: "continue"
BREAK: "break"
RETURN: "return"
WHILE: "while"
DO: "do"
IF: "if"
ELSE: "else"
FOR: "for"
AT: "@"
TRUE: "true"
FALSE: "false"
STRUCT: "struct"
INCLUDE: "#include"
BITSIZEOF: "#BitSizeOf"
ELEMSIZEOF: "#SizeInArrayOf"
COLONEQUALS: ":="
ASSUME: "assume"

COMMENT: /\/\/[^\r\n]*/

%ignore COMMENT
%ignore WS
%ignore "\n"
%ignore "\r"
WS: (" " | /\t/ )+

D: /[0-9]/
UD: /[_0-9]/
H: /[0-9a-fA-F]/
UH: /[_0-9a-fA-F]/

